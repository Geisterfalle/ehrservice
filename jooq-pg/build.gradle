plugins {
    id 'nu.studer.jooq' version '2.0.9'
}

apply plugin: 'java'

dependencies {
    // *** external dependencies ***
    compile "org.jooq:jooq:${versions.jooq}"
    compile "org.postgresql:postgresql:${versions.postgresql}"

    jooqRuntime "org.postgresql:postgresql:${versions.postgresql}"
}

jooq {
    version = "$versions.jooq"

    ecis(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = "jdbc:postgresql://${db.host}:${db.port}/${db.name}"
            user = db.user
            password = db.password
        }
        generator {
            database {
                name = 'org.jooq.util.postgres.PostgresDatabase'
                inputSchema = 'ehr'
            }
            target {
                packageName = 'com.ethercis.jooq.pg'
            }
            strategy {
                name = null
                matchers {
                    schemas {
                        schema {
                            schemaIdentifier {
                                transform = 'UPPER'
                                // Add _SCHEMA to schema identifiers as newer versions of jOOQ
                                // will add an _ to duplicate identifiers.
                                // This way the schema ehr will be named EHR_SCHEMA and the
                                // table ehr will be named EHR. Without this, the ehr schema
                                // will be named EHR and the table ehr will be named EHR_
                                // This was changed in jOOQ after version 3.7
                                expression = '\$0_SCHEMA'
                            }
                        }
                    }
                }
            }
        }
    }
}

tasks.generateEcisJooqSchemaSource.dependsOn(project(':db').tasks.flywayMigrate)